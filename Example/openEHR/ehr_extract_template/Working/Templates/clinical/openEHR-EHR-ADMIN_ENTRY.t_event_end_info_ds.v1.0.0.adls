template (adl_version=2.0.6; rm_release=1.0.2)
	openEHR-EHR-ADMIN_ENTRY.t_event_end_info_ds.v1.0.0

specialize
	openEHR-EHR-ADMIN_ENTRY.discharge.v1

language
	original_language = <[ISO_639-1::en]>

description
	lifecycle_state = <"unmanaged">
	original_author = <
		["name"] = <"Thomas Beale">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"thomas.beale@oceaninformatics.com">
		["date"] = <"01/04/2011">
	>
	copyright = <"copyright (c) 2011 openEHR Foundation">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Event end template overlay">
		>
	>

definition
	ADMIN_ENTRY[id1.1] matches {	-- Discharge event end information - Simple discharge summary
		/other_participations matches {
			PARTICIPATION[id85.1] occurrences matches {1} matches {	-- Responsible clinician on discharge
				performer existence matches {1} 
			}
			PARTICIPATION[id86] occurrences matches {0} 
			PARTICIPATION[id87] occurrences matches {0} 
		}
		/data[id4]/items matches {
			ELEMENT[id5.1] occurrences matches {1} 	-- Event end datetime
			ELEMENT[id81] occurrences matches {0} 
			ELEMENT[id61.1] 	-- Discharge from service/specialty
			ELEMENT[id72] occurrences matches {0} 
			allow_archetype CLUSTER[id75] closed 	-- Slot closed
			ELEMENT[id78.1] 	-- Discharge outcome
			ELEMENT[id79] occurrences matches {0} 
			ELEMENT[id80.1] occurrences matches {1} 	-- Discharge status/type
		}
	}

terminology
	term_definitions = <
		["en"] = <
			["id1.1"] = <
				text = <"Discharge event end information - Simple discharge summary">
				description = <"The administrative information at discharge of a patient from a period of care">
			>
			["id5.1"] = <
				text = <"Event end datetime">
				description = <"*">
			>
			["id61.1"] = <
				text = <"Discharge from service/specialty">
				description = <"*">
			>
			["id78.1"] = <
				text = <"Discharge outcome">
				description = <"*">
			>
			["id80.1"] = <
				text = <"Discharge status/type">
				description = <"*">
			>
			["id85.1"] = <
				text = <"Responsible clinician on discharge">
				description = <"Clinician responsible for discharge">
			>
		>
	>
